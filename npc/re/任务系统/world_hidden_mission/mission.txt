// ===== 【世界隐藏任务】【神之力】：任务流程 ================================ 


moc_fild07,33,352,6	script	残破的远古石碑	406,{
	
	mes "[残破的远古石碑]";
	mes "-------------------------------";
	mes "上面的字迹残破不堪，";
	mes "要继续看吗？";

	if(select("继续看:不看了") == 1) {
		next;
	} else {
		goto _TO_CLOSE;
	}

	mes "[残破的远古石碑]";
	mes "-------------------------------";
	mes "■■■ 我们一族历经数千年 ■■■";
	mes "或口耳相传 ■■■ 或亲眼见证";
	mes "或亲身经历 ■■■ 这世界曾出现过";
	mes "数百个拥有不同寻常体质的人■■■";
	mes "无迹可循的 ■■■ 或许更多 ■■■";

	if(select("继续看:不看了") == 1) {
		next;
	} else {
		goto _TO_CLOSE;
	}

	mes "[残破的远古石碑]";
	mes "-------------------------------";
	mes "他们并非天生特殊体质 ■■■";
	mes "或极强运 ■■■ 极努力 ■■■ 极恶";
	mes "■■■ 或极贪婪 ■■■ ";
	mes "都是后天 ■■■ 偶然获得 ■■■";
	mes "甚至某些人拥有复数特殊体质 ■■■";

	if(select("继续看:不看了") == 1) {
		next;
	} else {
		goto _TO_CLOSE;
	}

	mes "[残破的远古石碑]";
	mes "-------------------------------";
	mes "拥有这些体质的人 ■■■■■";
	mes "无一不是 ■■■ 当世最强者";
	mes "我们一族将其称之为 ^FF0000天选之子^000000";
	mes "这些特殊体质称之为 ^FF0000神之力^000000";
	
	if(select("继续看:不看了") == 1) {
		next;
	} else {
		goto _TO_CLOSE;
	}

	mes "[残破的远古石碑]";
	mes "------------------------";
	mes "^FF0000神之力^000000的获得方法 ■■";
	mes ".........";
	mes "..";
	mes ",";
	mes ".....";
	mes ".........";
	next;
	mes "后面的字迹好像被什么人抹去了";
	mes "";
	mes "但那些小点 ...";
	mes "像是后来再被隐晦补上去的";
	mes "^FF0000是为了把信息隐藏起来？^000000";

	// 此账号获得修炼【神之力】的资格
	if (#GOD_POWER_FLAG <= 0) {
		set #GOD_POWER_FLAG, 1;
	}
	goto _TO_CLOSE;

_TO_CLOSE:
	close2;
	close;
	end;
}



moc_fild07,92,59,6	script	微微凸起的小沙堆	557,1,1,{

	mes "一个小沙堆，要挖开吗？";
	switch(select("不挖:挖开")) {
		case 1:
			break;

		case 2:
			if (#GOD_POWER_FLAG <= 0) {
				mes "没有东西";

			} else if (#GOD_POWER_FLAG == 1) {
				mes "发现一个残破的木箱，要怎么办？";
				switch(select("用力砸开:轻轻撬开:重新埋起来")) {

					// 暴力性格
					case 1:
						mes "[薛定谔的破箱]";
						mes "------------------------";
						mes "砸开箱子的一刹那，";
						mes "似乎里面的东西性质发生的改变？";
						mes "... ...";
						mes "从箱子内发出耀眼强光 !!!";

						callfunc("f_assign_god_equipments", 0);
						set #GOD_POWER_FLAG, 2;
						break;

					// 温柔性格
					case 2:
						mes "[薛定谔的破箱]";
						mes "------------------------";
						mes "撬开箱子的一刹那，";
						mes "似乎里面的东西性质发生的改变？";
						mes "... ...";
						mes "从箱子内发出耀眼强光 !!!";

						callfunc("f_assign_god_equipments", 1);
						set #GOD_POWER_FLAG, 2;
						break;

					// 胆小性格
					case 3:
						mes "看上去有点可怕 ...";
						mes "可能有毒虫在里面 ...";
						mes "不义之财不可得，埋起来吧。";
						set #GOD_POWER_FLAG, 2;
						break;
				}
			} else if (#GOD_POWER_FLAG >= 2) {
				mes "已经什么都找不到了";
			}
			break;
	}

	close;
	end;
}
